!! Verb inflection
!  ---------------
!! The Hiligaynon language verbs inflect in persons.

LEXICON Verbs
@U.VOICE.ACT@ Active ;
@U.VOICE.OBJ@ Object ;
@U.VOICE.INS@ Instrumental ;
@U.VOICE.REC@ Recipient ;

LEXICON Active
@U.ASPECT.PFV@:@U.ASPECT.PFV@nag Verb_Root ;
@U.ASPECT.CON@:@U.ASPECT.CON@naga Verb_Root ;
@U.ASPECT.CTP@:@U.ASPECT.CTP@mag Verb_Root ;
@U.ASPECT.CTC@:@U.ASPECT.CTC@maga Verb_Root ;

LEXICON Object
@U.ASPECT.PFV@:@U.ASPECT.PFV@gin Verb_Root ;
@U.ASPECT.CON@:@U.ASPECT.CON@gina Verb_Root ;
@U.ASPECT.CTP@:@U.ASPECT.CTP@ Verb_Root ;
@U.ASPECT.CTC@:@U.ASPECT.CTC@paga Verb_Root ;

LEXICON Instrumental
@U.ASPECT.PFV@:@U.ASPECT.PFV@gin Verb_Root ;
@U.ASPECT.CON@:@U.ASPECT.CON@gina Verb_Root ;
@U.ASPECT.CTP@:@U.ASPECT.CTP@i Verb_Root ;
@U.ASPECT.CTC@:@U.ASPECT.CTC@iga Verb_Root ;

LEXICON Recipient
@U.ASPECT.PFV@:@U.ASPECT.PFV@gin Verb_Root ;
@U.ASPECT.CON@:@U.ASPECT.CON@gina Verb_Root ;
@U.ASPECT.CTP@:@U.ASPECT.CTP@ Verb_Root ;
@U.ASPECT.CTC@:@U.ASPECT.CTC@paga Verb_Root ;

LEXICON Voice
@U.VOICE.ACT@+V+Ind+ActV:@U.VOICE.ACT@%> Aspect ;
@U.VOICE.INS@+V+Ind+InsV:@U.VOICE.INS@%> Aspect ;
@U.VOICE.REC@+V+Ind+RecV:@U.VOICE.REC@%>an Aspect ;
@U.VOICE.OBJ@@U.ASPECT.PFV@+V+Ind+ObjV+Pfv:@U.VOICE.OBJ@@U.ASPECT.PFV@%> # ;
@U.VOICE.OBJ@@U.ASPECT.CON@+V+Ind+ObjV+Cont:@U.VOICE.OBJ@@U.ASPECT.CON@%> # ;
@U.VOICE.OBJ@@U.ASPECT.CTP@+V+Ind+ObjV+Ctpl:@U.VOICE.OBJ@@U.ASPECT.CTP@%>on # ;
@U.VOICE.OBJ@@U.ASPECT.CTC@+V+Ind+ObjV+Ctpl+Cont:@U.VOICE.OBJ@@U.ASPECT.CTC@%>on # ;

LEXICON Aspect
@U.ASPECT.PFV@+Pfv:@U.ASPECT.PFV@       Reduplication ;
@U.ASPECT.CON@+Cont:@U.ASPECT.CON@      Reduplication ;
@U.ASPECT.CTP@+Ctpl:@U.ASPECT.CTP@      Reduplication ;
@U.ASPECT.CTC@+Ctpl+Cont:@U.ASPECT.CTC@ Reduplication ;

LEXICON Reduplication
@R.REDUPLICATION.AUG@+Aug:@R.REDUPLICATION.AUG@ # ;
@R.REDUPLICATION.DIM@+Dim:@R.REDUPLICATION.DIM@ # ;
@R.REDUPLICATION.REP@+Iter:@R.REDUPLICATION.REP@ # ;
# ;



! vim: set ft=xfst-lexc:

!!€gt-norm: Verbs
!!€ naghambal:    hambal+V+Ind+ActV+Pfv
!!€ nagahambal:    hambal+V+Ind+ActV+Cont
!!€ maghambal:    hambal+V+Ind+ActV+Ctpl
!!€ magahambal:    hambal+V+Ind+ActV+Ctpl+Cont
!!€ ginhambal:    hambal+V+Ind+ObjV+Pfv
!!€ ginahambal:    hambal+V+Ind+ObjV+Cont
!!€ hambalon:    hambal+V+Ind+ObjV+Ctpl
!!€ pagahambalon:    hambal+V+Ind+ObjV+Ctpl+Cont
!!€ ginhambal:    hambal+V+Ind+InsV+Pfv
!!€ ginahambal:    hambal+V+Ind+InsV+Cont
!!€ ihambal:    hambal+V+Ind+InsV+Ctpl
!!€ igahambal:    hambal+V+Ind+InsV+Ctpl+Cont
!!€ ginhambalan:    hambal+V+Ind+RecV+Pfv
!!€ ginahambalan:    hambal+V+Ind+RecV+Cont
!!€ hambalan:    hambal+V+Ind+RecV+Ctpl
!!€ pagahambalan:    hambal+V+Ind+RecV+Ctpl+Cont

!!$gt-desc: Verbs
!!$ maghambalon:    hambal+V+Ind+ActV+ObjV+Ctpl
!!$ magihambal:    hambal+V+Ind+ActV+InsV+Ctpl
!!$ maghambalan:    hambal+V+Ind+ActV+RecV+Ctpl
!!$ ihambalon:    hambal+V+Ind+ObjV+InsV+Ctpl
!!$ hambalonan:    hambal+V+Ind+ObjV+RecV+Ctpl
!!$ ihambalan:    hambal+V+Ind+InsV+RecV+Ctpl
!!$ naganaghambal:    hambal+V+Ind+ActV+Pfv+Cont
!!$ nagmaghambal:    hambal+V+Ind+ActV+Pfv+Ctpl
!!$ ginginahambal:    hambal+V+Ind+ObjV+Pfv+Cont
!!$ ginhambalon:    hambal+V+Ind+ObjV+Pfv+Ctpl
!!$ ginihambal:    hambal+V+Ind+InsV+Pfv+Cont
!!$ ginihambal:    hambal+V+Ind+InsV+Pfv+Ctpl
!!$ ginginahambalan:    hambal+V+Ind+RecV+Pfv+Cont
!!$ ginhambalan:    hambal+V+Ind+RecV+Pfv+Ctpl

!!€gt-desc: Reduplication
!!€ naghambal-hambal: hambal+V+Ind+ActV+Pfv+Iter
!!€ naghambal-hambal: hambal+V+Ind+ActV+Pfv+Aug
!!€ naghambal-hambal: hambal+V+Ind+ActV+Pfv+Dim
!!€ nagahambal-hambal: hambal+V+Ind+ActV+Cont+Iter
!!€ nagahambal-hambal: hambal+V+Ind+ActV+Cont+Aug
!!€ nagahambal-hambal: hambal+V+Ind+ActV+Cont+Dim
!!€ maghambal-hambal: hambal+V+Ind+ActV+Ctpl+Iter
!!€ maghambal-hambal: hambal+V+Ind+ActV+Ctpl+Aug
!!€ maghambal-hambal: hambal+V+Ind+ActV+Ctpl+Dim
!!€ magahambal-hambal: hambal+V+Ind+ActV+Ctpl+Cont+Iter
!!€ magahambal-hambal: hambal+V+Ind+ActV+Ctpl+Cont+Aug
!!€ magahambal-hambal: hambal+V+Ind+ActV+Ctpl+Cont+Dim
!!€ magadala-dalagan: dalagan+V+Ind+ActV+Ctpl+Cont+Dim


!!$gt-desc: Reduplication
!!$ magadalagan-dalagan: dalagan+V+Ind+ActV+Ctpl+Cont+Dim
!!$ magadalag-dalagan: dalagan+V+Ind+ActV+Ctpl+Cont+Dim
!!$ magadalagan-dala: dalagan+V+Ind+ActV+Ctpl+Cont+Dim
!!$ magadal-dalagan: dalagan+V+Ind+ActV+Ctpl+Cont+Dim
!!$ magadaladalagan: dalagan+V+Ind+ActV+Ctpl+Cont+Dim

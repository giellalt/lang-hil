!! Verb inflection
!  ---------------
!! The Hiligaynon language verbs inflect in persons.

LEXICON Verbs
@P.VOICE.ACT@   Active ;
@P.VOICE.OBJ@   Object ;
@P.VOICE.INS@   Instrumental ;
@P.VOICE.REC@   Recipient ;
                V2 ;

LEXICON Active
@P.ASPECT.PFV@:@P.ASPECT.PFV@nag    V2 ;
@P.ASPECT.CON@:@P.ASPECT.CON@naga   V2 ;
@P.ASPECT.CTP@:@P.ASPECT.CTP@mag    V2 ;
@P.ASPECT.CTC@:@P.ASPECT.CTC@maga   V2 ;

LEXICON Object
@P.ASPECT.PFV@:@P.ASPECT.PFV@gin    V2 ;
@P.ASPECT.CON@:@P.ASPECT.CON@gina   V2 ;
@P.ASPECT.CTP@:@P.ASPECT.CTP@       V2 ;
@P.ASPECT.CTC@:@P.ASPECT.CTC@paga   V2 ;

LEXICON Instrumental
@P.ASPECT.PFV@:@P.ASPECT.PFV@gin    V2 ;
@P.ASPECT.CON@:@P.ASPECT.CON@gina   V2 ;
@P.ASPECT.CTP@:@P.ASPECT.CTP@i      V2 ;
@P.ASPECT.CTC@:@P.ASPECT.CTC@iga    V2 ;

LEXICON Recipient
@P.ASPECT.PFV@:@P.ASPECT.PFV@gin    V2 ;
@P.ASPECT.CON@:@P.ASPECT.CON@gina   V2 ;
@P.ASPECT.CTP@:@P.ASPECT.CTP@       V2 ;
@P.ASPECT.CTC@:@P.ASPECT.CTC@paga   V2 ;

LEXICON V2
@P.MOOD.HAB@:@P.MOOD.HAB@pa%^N  V3 ;
                                V3 ;

LEXICON V3
@P.PREFIX.CAU@:@P.PREFIX.CAU@pa Verb_Root ;
                                Verb_Root ;

LEXICON Voice
@R.VOICE.ACT@+V+Ind+ActV:@R.VOICE.ACT@%>                                            Aspect ;
@R.VOICE.INS@+V+Ind+InsV:@R.VOICE.INS@%>                                            Aspect ;
@R.VOICE.REC@+V+Ind+RecV:@R.VOICE.REC@%>an                                          Aspect ;
@R.VOICE.OBJ@@R.ASPECT.PFV@+V+Ind+ObjV+Pfv:@R.VOICE.OBJ@@R.ASPECT.PFV@%>            # ;
@R.VOICE.OBJ@@R.ASPECT.CON@+V+Ind+ObjV+Cont:@R.VOICE.OBJ@@R.ASPECT.CON@%>           # ;
@R.VOICE.OBJ@@R.ASPECT.CTP@+V+Ind+ObjV+Ctpl:@R.VOICE.OBJ@@R.ASPECT.CTP@%>on         # ;
@R.VOICE.OBJ@@R.ASPECT.CTC@+V+Ind+ObjV+Ctpl+Cont:@R.VOICE.OBJ@@R.ASPECT.CTC@%>on    # ;
+V+Inf:                                                                             # ;
                                                                                    Aspect ;

LEXICON Aspect
@R.ASPECT.PFV@+Pfv:@R.ASPECT.PFV@       V2_Tags ;
@R.ASPECT.CON@+Cont:@R.ASPECT.CON@      V2_Tags ;
@R.ASPECT.CTP@+Ctpl:@R.ASPECT.CTP@      V2_Tags ;
@R.ASPECT.CTC@+Ctpl+Cont:@R.ASPECT.CTC@ V2_Tags ;
                                        V2_Tags ;

LEXICON V2_Tags
@R.MOOD.HAB@+Hab:@R.MOOD.HAB@   V3_Tags ;
                                V3_Tags ;

LEXICON V3_Tags
@R.PREFIX.CAU@+Caus:@R.PREFIX.CAU@  Reduplication ;
                                    Reduplication ;

LEXICON Reduplication
!!@R.REDUPLICATION.AUG@+Aug:@R.REDUPLICATION.AUG@     # ;
!!@R.REDUPLICATION.DIM@+Dim:@R.REDUPLICATION.DIM@     # ;
!!@R.REDUPLICATION.REP@+Iter:@R.REDUPLICATION.REP@    # ;
                                                    # ;



! vim: set ft=xfst-lexc:

!!€gt-norm: Verbs
!!€ naghambal:    hambal+V+Ind+ActV+Pfv
!!€ nagahambal:    hambal+V+Ind+ActV+Cont
!!€ maghambal:    hambal+V+Ind+ActV+Ctpl
!!€ magahambal:    hambal+V+Ind+ActV+Ctpl+Cont
!!€ ginhambal:    hambal+V+Ind+ObjV+Pfv
!!€ ginahambal:    hambal+V+Ind+ObjV+Cont
!!€ hambalon:    hambal+V+Ind+ObjV+Ctpl
!!€ pagahambalon:    hambal+V+Ind+ObjV+Ctpl+Cont
!!€ ginhambal:    hambal+V+Ind+InsV+Pfv
!!€ ginahambal:    hambal+V+Ind+InsV+Cont
!!€ ihambal:    hambal+V+Ind+InsV+Ctpl
!!€ igahambal:    hambal+V+Ind+InsV+Ctpl+Cont
!!€ ginhambalan:    hambal+V+Ind+RecV+Pfv
!!€ ginahambalan:    hambal+V+Ind+RecV+Cont
!!€ hambalan:    hambal+V+Ind+RecV+Ctpl
!!€ pagahambalan:    hambal+V+Ind+RecV+Ctpl+Cont

!!$gt-desc: Verbs
!!$ maghambalon:    hambal+V+Ind+ActV+ObjV+Ctpl
!!$ magihambal:    hambal+V+Ind+ActV+InsV+Ctpl
!!$ maghambalan:    hambal+V+Ind+ActV+RecV+Ctpl
!!$ ihambalon:    hambal+V+Ind+ObjV+InsV+Ctpl
!!$ hambalonan:    hambal+V+Ind+ObjV+RecV+Ctpl
!!$ ihambalan:    hambal+V+Ind+InsV+RecV+Ctpl
!!$ naganaghambal:    hambal+V+Ind+ActV+Pfv+Cont
!!$ nagmaghambal:    hambal+V+Ind+ActV+Pfv+Ctpl
!!$ ginginahambal:    hambal+V+Ind+ObjV+Pfv+Cont
!!$ ginhambalon:    hambal+V+Ind+ObjV+Pfv+Ctpl
!!$ ginihambal:    hambal+V+Ind+InsV+Pfv+Cont
!!$ ginihambal:    hambal+V+Ind+InsV+Pfv+Ctpl
!!$ ginginahambalan:    hambal+V+Ind+RecV+Pfv+Cont
!!$ ginhambalan:    hambal+V+Ind+RecV+Pfv+Ctpl

!!€gt-desc: Reduplication
!!€ naghambal-hambal: hambal+V+Ind+ActV+Pfv+Iter
!!€ naghambal-hambal: hambal+V+Ind+ActV+Pfv+Aug
!!€ naghambal-hambal: hambal+V+Ind+ActV+Pfv+Dim
!!€ nagahambal-hambal: hambal+V+Ind+ActV+Cont+Iter
!!€ nagahambal-hambal: hambal+V+Ind+ActV+Cont+Aug
!!€ nagahambal-hambal: hambal+V+Ind+ActV+Cont+Dim
!!€ maghambal-hambal: hambal+V+Ind+ActV+Ctpl+Iter
!!€ maghambal-hambal: hambal+V+Ind+ActV+Ctpl+Aug
!!€ maghambal-hambal: hambal+V+Ind+ActV+Ctpl+Dim
!!€ magahambal-hambal: hambal+V+Ind+ActV+Ctpl+Cont+Iter
!!€ magahambal-hambal: hambal+V+Ind+ActV+Ctpl+Cont+Aug
!!€ magahambal-hambal: hambal+V+Ind+ActV+Ctpl+Cont+Dim
!!€ magadala-dalagan: dalagan+V+Ind+ActV+Ctpl+Cont+Dim


!!$gt-desc: Reduplication
!!$ magadalagan-dalagan: dalagan+V+Ind+ActV+Ctpl+Cont+Dim
!!$ magadalag-dalagan: dalagan+V+Ind+ActV+Ctpl+Cont+Dim
!!$ magadalagan-dala: dalagan+V+Ind+ActV+Ctpl+Cont+Dim
!!$ magadal-dalagan: dalagan+V+Ind+ActV+Ctpl+Cont+Dim
!!$ magadaladalagan: dalagan+V+Ind+ActV+Ctpl+Cont+Dim
